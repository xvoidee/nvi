" vi:syntax=vim

let g:lightline = {
  \ 'active': {
  \   'left': [
  \     [ 'mode', 'paste' ],
  \     [ 'gitbranch', 'readonly', 'filename', 'modified' ]
  \   ]
  \ },
  \ 'component_function': {
  \   'gitbranch'  : 'fugitive#head',
  \   'filename'   : 'NviFilename',
  \   'filetype'   : 'NviFiletype',
  \   'fileformat' : 'NviFileformat'
  \ },
  \ 'tabline' : {
  \   'left'  : [ [ 'buffers' ] ],
  \   'right' : [ [ ] ]
  \ },
  \ 'component_expand' : { 'buffers' : 'lightline#bufferline#buffers' },
  \ 'component_type'   : { 'buffers' : 'tabsel' },
  \ 'separator' : {
  \   'left'  : g:nvi_lightline_separator_left,
  \   'right' : g:nvi_lightline_separator_right
  \ },
  \ 'subseparator' : {
  \   'left'  : g:nvi_lightline_subseparator_left,
  \   'right' : g:nvi_lightline_subseparator_right
  \ }
\ }

" Make file name relative to project root (.git)
function! NviFilename()
  let root = fnamemodify(get(b:, 'git_dir'), ':h')
  let path = expand('%:p')
  if path[:len(root)-1] ==# root
    return path[len(root)+1:]
  endif
  return expand('%')
endfunction

" Nerdify status line (add icons)
if g:nvi_nerd_fonts == 1
  let g:lightline#bufferline#enable_devicons = 1

  function! NviFiletype()
    return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype . ' ' . WebDevIconsGetFileTypeSymbol() : 'no ft') : ''
  endfunction

  function! NviFileformat()
    return winwidth(0) > 70 ? (&fileformat . ' ' . WebDevIconsGetFileFormatSymbol()) : ''
  endfunction
else
  function! NviFiletype()
    return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype . ' ' : 'no ft') : ''
  endfunction

  function! NviFileformat()
    return winwidth(0) > 70 ? (&fileformat . ' ') : ''
  endfunction
endif
